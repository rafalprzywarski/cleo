(ns cleo.core)


(def reverse1
  (fn* reverse1 [xs]
    (loop* [r () s (seq xs)]
      (if s
        (recur (conj* r (first s)) (next s))
        r))))


(def map1
  (fn* map1 [f s]
    (reverse1
      (loop* [m () s (seq s)]
        (if s
          (recur (conj* m (f (first s))) (next s))
          m)))))


(def reduce1
  (fn* [f val coll]
    (let* [s (seq coll)]
      (if s
        (recur f (f val (first s)) (next s))
        val))))


(def nnext
  (fn* nnext [x] (next (next x))))


(def wrapdo*
  (fn* wrapdo* [body]
    (if (next body)
      (if (nnext body)
        (list (first body) (conj* (next body) 'do))
        body)
      (list (first body)))))


(def defmacro
  (macro* defmacro [name & decl]
    (list 'def name (if (vector? (first decl))
                      `(macro* ~name ~@(wrapdo* decl))
                      `(macro* ~name ~@(map1 wrapdo* decl))))))


(def let
  (macro* [bindings & body]
    `(let* ~bindings ~@(if body
                         (if (next body)
                           (list (conj* body 'do))
                           body)
                         ()))))


(defmacro fn [& decl]
  (let* [name (if (symbol? (first decl)) [(first decl)] [])
         decl (if (symbol? (first decl)) (next decl) decl)]
    (if (vector? (first decl))
      `(fn* ~@name ~@(wrapdo* decl))
      `(fn* ~@name ~@(map1 wrapdo* decl)))))


(defmacro defn [name & decl] (list 'def name `(fn ~name ~@decl)))


(defmacro not [x] `(if ~x nil :true))


(defmacro or
  ([] nil)
  ([x] x)
  ([x & xs] `(let [x# ~x] (if x# x# (or ~@xs)))))


(defmacro and
  ([] :true)
  ([x] x)
  ([x & xs] `(let [x# ~x] (if x# (and ~@xs) x#))))


(defn seq? [x] (isa? (type x) cleo.core/Sequence))


(defn second [x] (first (next x)))


(defmacro when [expr & body]
  `(if ~expr (do ~@body)))


(defmacro cond [& pairs]
  (when pairs
    (if (not (next pairs))
      (throw (new IllegalArgument  "cond requires an even number of forms"))
      `(if ~(first pairs) ~(second pairs) (cond ~@(nnext pairs))))))


(defn bit-and
  ([x y] (bit-and* x y))
  ([x y & more]
      (reduce1 bit-and* (bit-and* x y) more)))


(defn bit-or
  ([x y] (bit-or* x y))
  ([x y & more]
      (reduce1 bit-or* (bit-or* x y) more)))


(defn bit-xor
  ([x y] (bit-xor* x y))
  ([x y & more]
      (reduce1 bit-xor* (bit-xor* x y) more)))


(defn bit-and-not
  ([x y] (bit-and-not* x y))
  ([x y & more]
      (reduce1 bit-and-not* (bit-and-not* x y) more)))


(defn dec [x] (- x 1))
(defn inc [x] (+ x 1))
(defn zero? [x] (= 0 x))
(defn pos? [x] (< 0 x))
(defn neg? [x] (< x 0))
(defn int? [x] (identical? (type x) Int64))
(defn even? [x] (zero? (bit-and* x 1)))
(defn odd? [x] (not (even? x)))


(defn conj
  ([] [])
  ([coll] coll)
  ([coll x] (conj* coll x))
  ([coll x & xs]
    (if xs
      (recur (conj* coll x) (first xs) (next xs))
      (conj* coll x))))
