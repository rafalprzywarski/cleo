(ns cleo.core)


(def reverse1
  (fn* reverse1 [xs]
    (loop* [r () s (seq xs)]
      (if s
        (recur (conj r (first s)) (next s))
        r))))

(def revmap1
  (fn* revmap1 [f s]
    (reverse1
      (loop* [m () s (seq s)]
        (if s
          (recur (conj m (f (first s))) (next s))
          m)))))


(def nnext
  (fn* nnext [x] (next (next x))))


(def wrapdo*
  (fn* wrapdo* [body]
    (if (next body)
      (if (nnext body)
        (list (first body) (conj (next body) 'do))
        body)
      (list (first body)))))


(def defmacro
  (macro* defmacro [name & decl]
    (list 'def name (if (vector? (first decl))
                      `(macro* ~name ~@(wrapdo* decl))
                      `(macro* ~name ~@(revmap1 wrapdo* decl))))))


(def let
  (macro* [bindings & body]
    `(let* ~bindings ~@(if body
                         (if (next body)
                           (list (conj body 'do))
                           body)
                         ()))))


(defmacro fn [& decl]
  (let* [name (if (symbol? (first decl)) [(first decl)] [])
         decl (if (symbol? (first decl)) (next decl) decl)]
    (if (vector? (first decl))
      `(fn* ~@name ~@(wrapdo* decl))
      `(fn* ~@name ~@(revmap1 wrapdo* decl)))))


(defmacro defn [name & decl] (list 'def name `(fn ~name ~@decl)))


(defmacro not [x] `(if ~x nil :true))


(defmacro or
  ([] nil)
  ([x] x)
  ([x & xs] `(let [x# ~x] (if x# x# (or ~@xs)))))


(defmacro and
  ([] :true)
  ([x] x)
  ([x & xs] `(let [x# ~x] (if x# (and ~@xs) x#))))


(defn seq? [x] (isa? (type x) cleo.core/Sequence))


(defn second [x] (first (next x)))


(defmacro when [expr & body]
  `(if ~expr (do ~@body)))


(defmacro cond [& pairs]
  (when pairs
    (if (not (next pairs))
      (throw (new IllegalArgument  "cond requires an even number of forms"))
      `(if ~(first pairs) ~(second pairs) (cond ~@(nnext pairs))))))


(defn dec [x] (- x 1))
(defn inc [x] (+ x 1))
(defn zero? [x] (= 0 x))
(defn pos? [x] (< 0 x))
(defn neg? [x] (< x 0))
(defn int? [x] (identical? (type x) Int64))
